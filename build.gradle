group 'io.github.carlanton.demo'
version '1.0-SNAPSHOT'

task wrapper(type: Wrapper) {
    gradleVersion = '4.0.1'
    distributionUrl = "https://services.gradle.org/distributions/gradle-$gradleVersion-all.zip"
}

apply plugin: 'java'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compileOnly 'org.projectlombok:lombok:1.16.18'
    testCompile group: 'junit', name: 'junit', version: '4.11'
}



task delombok {
    def srcDelomboked = 'build/src-delomboked'
    def srcJava = 'src/main/java'

    // delombok task may depend on other projects already being compiled
    dependsOn configurations.compile.getTaskDependencyFromProjectDependency(true, "compileJava")

    // Set up incremental build, must be made in the configuration phase (not doLast)
    inputs.files file(srcJava)
    outputs.dir file(srcDelomboked)

    doLast {
        def collection = files(configurations.compile)
        def sumTree = collection + fileTree(dir: 'bin')

        ant.taskdef(name: 'delombok',
                classname: 'lombok.delombok.ant.Tasks$Delombok', classpath: configurations.compileOnly.asPath)
        ant.delombok(from:srcJava, to:srcDelomboked, classpath: sumTree.asPath)
    }
}

task sourcesJar(type: Jar, dependsOn: delombok) {
    classifier = 'sources'
    from 'build/src-delomboked'
}

artifacts {
    archives sourcesJar
}